buildscript {
	repositories {
		maven {url "https://maven.google.com"}
		maven {url "https://jcenter.bintray.com"}
		mavenCentral()
		google()
	}
	
	dependencies {
		classpath 'com.android.tools.build:gradle:7.1.3'
		classpath 'com.google.gms:google-services:4.3.10'
	}
}

apply plugin: 'com.android.application'

repositories {
	maven {url "https://maven.google.com"}
	maven {url "https://jcenter.bintray.com"}
	mavenCentral()
	google()
}


def assetDir = "assets"
def properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    assetDir = properties.getProperty("asset.dir", "assets")
}


android {
	compileSdkVersion 30
	buildToolsVersion "29.0.3"
	useLibrary 'org.apache.http.legacy'
	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = [assetDir]
			jniLibs {
				srcDir 'libs'
			}
		}
		
		androidTest.setRoot('tests')
		debug.setRoot('build-types/debug')
		release.setRoot('build-types/release')
	}
	
	dexOptions {
		preDexLibraries = false
	}
	
	defaultConfig{
		minSdkVersion 18
		targetSdkVersion 30
		multiDexEnabled true
	}
	packagingOptions {
		exclude "META-INF/LICENSE"
		exclude "META-INF/DEPENDENCIES"
	}	
	lintOptions {
		checkReleaseBuilds false
		abortOnError false
	}
	
	if (KEYSTORE_PATH?.trim()) {
		signingConfigs{
			releaseWithSignedKey {
				storeFile file(KEYSTORE_PATH)
				storePassword KEYSTORE_PW
				keyAlias KEYSTORE_ALIAS
				keyPassword KEYSTORE_ALIAS_PW
			}
		}
	}
	
	buildTypes {
		release {
			//minifyEnabled true
			//proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'morpheus_proguard-project.txt'
			if (KEYSTORE_PATH?.trim()) {
				signingConfig signingConfigs.releaseWithSignedKey
			}
		}
	}
}

//추가
repositories {
    flatDir {
        dirs 'libs', 'mcoreLibs'
    }
}



dependencies {
	implementation fileTree(dir: 'mcoreLibs', excludes: ['android-support-v4.jar', 'gson.jar', 'apache.jar'],  include: '*.jar')
	
	
	/**
	* ==========================================
	* 외부라이브러리 적용시  [example.aar file]
	* ==========================================
	* implementation name: 'example-release', ext: 'aar'
	*/
	implementation name: 'ute_sdk_v2.9.4', ext: 'aar'
	implementation 'com.google.code.gson:gson:2.8.9'
	implementation 'com.google.firebase:firebase-messaging:20.2.4'
	implementation 'com.firebase:firebase-jobdispatcher:0.8.6'
	implementation 'androidx.legacy:legacy-support-v4:1.0.0'
	implementation 'androidx.multidex:multidex:2.0.1'
}

import org.apache.tools.ant.taskdefs.condition.Os

task  AppRunDebug (type: Exec, dependsOn: 'installDebug') {
	if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		commandLine 'cmd', '/c', android.getAdbExe().toString(), 'shell', 'am', 'start', '-n', 'iitp.infection.pm/.Startup'
	}
	else if (Os.isFamily(Os.FAMILY_MAC)) {
		commandLine android.getAdbExe().toString(), "shell", 'am', 'start', '-n', 'iitp.infection.pm/.Startup'
	}
}

apply plugin: 'com.google.gms.google-services'

